main      = _{
  SOI ~ ( 
    type_ |
    pipeline |
    issue
  ) ~ NEWLINE* ~ EOI
}
s         = _{ SPACE_SEPARATOR+ }
n         = _{ NEWLINE+ ~ PEEK_ALL }
i         = _{ n ~ PUSH((" " | "\t")+) }
COMMENT   = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
options   =  { "options"   ~ i ~ keyed_formats ~ DROP }
variables =  { "variables" ~ i ~ keyed_formats ~ DROP }
result    =  { "result"    ~ i ~ keyed_formats ~ DROP }

// HEADER
header               =  {
  s ~ id_model_recipe ~ n ~
  header_text? ~ header_text? ~ header_icon? ~ header_separator_big
}
header_text          = _{ header_separator ~ ( text | object ) ~ n }
header_icon          = _{ header_separator ~ id_recipe ~ n }
header_separator_big = _{ "===" ~ "="* ~ n }
header_separator     = _{ "---" ~ "-"* ~ n }

// VALUES
value         =  { (s ~ value_inline) | (i ~ value_newline ~ DROP) }
value_inline  = _{ setter | getter | quantity | boolean | text }
value_newline = _{ list | object }
list          =  { list_item ~ (n ~ list_item)* }
list_item     = _{ value_inline | ("\\" ~ i ~ value_newline ~ DROP) }
object        =  { key_value ~ ( n ~ key_value )* }
key_value     =  { key ~ value }
getter        =  { "$" ~ query }
boolean       =  { "true" | "false" }
quantity      =  @{
  "-"?
  ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
  ~ ("." ~ ASCII_DIGIT*)?
  ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}// TODO: decimals (copy json)
text          = _{ "\"" ~ text_inner ~ "\"" }
text_inner    = @{ char* }
char          =  {
  !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

// SETTERS
setter          =  { setter_not | setter_long | setter_short }
setter_short    =  {
  "(" ~ s?
    ~ value_inline ~ s? ~ setter_operator ~ s? ~ value_inline
  ~ s? ~ ")"
}
setter_long     =  { "~" ~ key ~ value? }
setter_not      =  { "!" ~ value_inline }
setter_operator =  { comparator | arithmetic | logic }
comparator      = _{ "=" | "!" | ">=" | "<=" | ">" | "<" }
arithmetic      = _{ "+" | "-" | "/" | "*" | "^" | "%" }
logic           = _{ "|" | "&" }

// KEYS
id_recipe         =  { id_recipe_ | id_short }
id_recipe_        = _{ key_id ~ ":" ~ namespaced }
id_model_version_ = _{ id_recipe_ ~ ":" ~ namespaced }
id_model_version  =  { id_model_version_ | id_short }
id_model_recipe_  = _{ id_model_version_ ~ ":" ~ namespaced }
id_model_recipe   =  { id_model_recipe_ | id_short }
id_short          = ${ ":" ~ namespaced }
namespaced        =  { key_id ~ ( "/" ~ key_id )* }
query             =  { key ~ ( "." ~ key )* }
set_append        =  { ".+" }
set_query         =  { query ~ set_append? }
key               = @{ key_char+ | "''" }
key_id            =  { (key_char | ".")+ }
key_char          = _{
  'À'..'Ö' | 'Ø'..'ö' | 'ø'..'ÿ'| ASCII_ALPHANUMERIC | "-" | "_"
}

// HANDLERS
handlers           = _{ "%" ~ n ~ sub_handlers }
handler            = _{ set | print | handler_if | iterate }
set                =  { "set" ~ s ~ set_query ~ value }
print              =  { "print" ~ value }
handler_if         =  {
  "if" ~ value ~ i ~ handlers ~ DROP ~ handler_else?
}
handler_else_start = _{ n ~ "else" }
handler_else       =  {
  &handler_else_start ~ handler_else_start ~ i ~ handlers ~ DROP
}
iterate            = {
  "iterate" ~ s ~ iterate_key ~ ("," ~ s ~ iterate_value)? ~ s 
  ~ "in" ~ value ~ i ~ handlers ~ DROP
}
iterate_key        =  { iterate_ignore | query }
iterate_value      =  { iterate_ignore | query }
iterate_ignore     =  { "_" }
sub_handlers       =  { handler ~ ( n ~ handler )* }









// PIPELINES
pipeline           =  { "pipelines" ~ header ~ pipeline_content }
pipeline_content   =  { options? ~ variables? ~ result? }





// TYPES
type_            =  { "types" ~ header ~ type_content }
type_content     =  { type_templates }
type_templates   =  { "templates" ~ i ~ keyed_formats }
keyed_formats    = _{ keyed_format ~ (n ~ keyed_format)* }
keyed_format     =  { key ~ s ~ format }
format           =  {
  ((&id_model_recipe ~ id_model_recipe) | key) ~
  format_inputs? ~ subformat? ~ format_default?
}
unknown_format   =  { // keyed or not
  (&id_model_recipe ~ id_model_recipe) | key ~ (s ~ key)? ~
  format_inputs? ~ subformat? ~ format_default?
}
unknown_formats  = _{
  unknown_format ~ (n ~ unknown_format)*
}
subformat        =  { &(n ~ (" "|"\t")+) ~ i ~ unknown_formats ~ DROP }
format_inputs    = _{ (s ~ format_flag)? ~ format_options? }
format_flag      = @{ "#" ~ key }
format_options   =  {
  &(n ~ (" "|"\t")+ ~ "(") ~ i ~ "(" ~ i ~ object ~ DROP ~ n ~ ")" ~ DROP
}
format_default   = _{ (s ~ "=" ~ value) | (&(n ~ "=") ~ n ~ "=" ~ value) }
format_templates =  { "<" ~ key ~ (s ~ key)* ~ ">" }






// ISSUES
issue           =  { "issues" ~ header ~ issue_content }
issue_content   =  { issue_level ~ n ~ issue_format ~ n ~ issue_http? }
issue_level     =  { "level" ~ s ~ issue_levels }
issue_levels    =  { "ERROR" | "WARNING" | "FATAL" }
issue_format    =  { "format" ~ s ~ format }
issue_http      =  { "http" ~ i ~ "code" ~ s ~ issue_http_code ~ DROP }
issue_http_code =  { "EXPECTATION_FAILED" }

